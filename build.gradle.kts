/*
Copyright 2022 c-fraser

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

    https://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
*/
import com.diffplug.gradle.spotless.SpotlessExtension
import com.diffplug.gradle.spotless.SpotlessTask
import io.github.gradlenexus.publishplugin.NexusPublishExtension
import io.gitlab.arturbosch.detekt.Detekt
import kotlin.collections.filter
import kotlinx.knit.KnitPlugin
import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent
import org.gradle.internal.extensions.stdlib.capitalized
import org.jetbrains.dokka.base.DokkaBase
import org.jetbrains.dokka.base.DokkaBaseConfiguration
import org.jetbrains.dokka.gradle.DokkaTask
import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.tasks.KotlinTest
import org.jreleaser.gradle.plugin.JReleaserExtension
import org.jreleaser.model.Active

buildscript {
  repositories { mavenCentral() }
  dependencies { classpath(libs.knit) }
  configurations.classpath {
    resolutionStrategy {
      // See https://github.com/jreleaser/jreleaser/issues/1643
      force("org.eclipse.jgit:org.eclipse.jgit:5.13.0.202109080827-r")
    }
  }
}

plugins {
  alias(libs.plugins.kotlin.multiplatform)
  alias(libs.plugins.dokka)
  alias(libs.plugins.spotless)
  alias(libs.plugins.detekt)
  alias(libs.plugins.nexus.publish)
  alias(libs.plugins.jreleaser)
  alias(libs.plugins.dependency.versions)
  alias(libs.plugins.kover)
  alias(libs.plugins.compatibility.validator)
  `maven-publish`
  signing
}

apply<KnitPlugin>()

allprojects {
  group = "io.github.c-fraser"
  version = "1.0.0"

  repositories { mavenCentral() }
}

kotlin {
  jvm { compilerOptions { jvmTarget.set(JvmTarget.JVM_17) } }
  js {
    browser()
    nodejs()
  }
  linuxX64()
  linuxArm64()
  mingwX64()
  macosArm64()
  macosX64()
  iosX64()
  iosArm64()
  iosSimulatorArm64()
  watchosDeviceArm64()
  watchosSimulatorArm64()
  watchosX64()
  watchosArm32()
  watchosArm64()
  tvosSimulatorArm64()
  tvosX64()
  tvosArm64()
  androidNativeX64()
  androidNativeX86()
  androidNativeArm32()
  androidNativeArm64()

  sourceSets @Suppress("unused") {
    val commonTest by getting {
      dependencies {
        implementation(libs.kotest.assertions.core)
        implementation(libs.kotest.framework.engine)
      }
    }
    val jvmTest by getting {
      dependencies {
        implementation(libs.kotest.runner.junit5)
        implementation(libs.knit.test)
      }
    }
  }
}

val kotlinSourceFiles by lazy {
  fileTree(rootProject.rootDir) {
    // Exclude the files automatically generated by `kotlinx-knit`
    exclude("src/jvmTest/kotlin/io/github/cfraser/connekt/knit/**/*.kt")
    include("src/**/*.kt")
  }
}

configure<SpotlessExtension> {
  val ktfmtVersion = libs.versions.ktfmt.get()
  val licenseHeader =
      """
      /*
      Copyright 2022 c-fraser
      
      Licensed under the Apache License, Version 2.0 (the "License");
      you may not use this file except in compliance with the License.
      You may obtain a copy of the License at
      
          https://www.apache.org/licenses/LICENSE-2.0
      
      Unless required by applicable law or agreed to in writing, software
      distributed under the License is distributed on an "AS IS" BASIS,
      WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
      See the License for the specific language governing permissions and
      limitations under the License.
      */
      """
          .trimIndent()

  kotlin {
    ktfmt(ktfmtVersion)
    licenseHeader(licenseHeader)
    target(kotlinSourceFiles)
  }

  kotlinGradle {
    ktfmt(ktfmtVersion)
    licenseHeader(licenseHeader, "(import|plugins|rootProject)")
    target(fileTree(rootProject.rootDir) { include("**/*.gradle.kts") })
  }
}

detekt {
  buildUponDefaultConfig = true
  allRules = true
  config.setFrom(rootDir.resolve("detekt.yml"))
  source.setFrom(kotlinSourceFiles)
}

publishing {
  val javadocJar by
      tasks.registering(Jar::class) {
        val dokkaHtml by tasks.getting(DokkaTask::class)
        dependsOn(dokkaHtml)
        archiveClassifier.set("javadoc")
        from(tasks.dokkaHtml.flatMap { it.outputDirectory })
      }

  publications.withType<MavenPublication> {
    artifact(javadocJar)
    pom {
      name.set(project.name)
      description.set("${project.name}-${project.version}")
      url.set("https://github.com/c-fraser/connekt")
      inceptionYear.set("2022")

      issueManagement {
        system.set("GitHub")
        url.set("https://github.com/c-fraser/connekt/issues")
      }

      licenses {
        license {
          name.set("The Apache Software License, Version 2.0")
          url.set("https://www.apache.org/licenses/LICENSE-2.0.txt")
          distribution.set("repo")
        }
      }

      developers {
        developer {
          id.set("c-fraser")
          name.set("Chris Fraser")
        }
      }

      scm {
        url.set("https://github.com/c-fraser/connekt")
        connection.set("scm:git:git://github.com/c-fraser/connekt.git")
        developerConnection.set("scm:git:ssh://git@github.com/c-fraser/connekt.git")
      }
    }
  }

  signing {
    publications.withType<MavenPublication>().all mavenPublication@{
      useInMemoryPgpKeys(System.getenv("GPG_SIGNING_KEY"), System.getenv("GPG_PASSWORD"))
      sign(this@mavenPublication)
    }
  }
}

configure<NexusPublishExtension> {
  repositories {
    sonatype {
      nexusUrl.set(uri("https://ossrh-staging-api.central.sonatype.com/service/local/"))
      snapshotRepositoryUrl.set(uri("https://central.sonatype.com/repository/maven-snapshots/"))
      username.set(System.getenv("SONATYPE_USERNAME"))
      password.set(System.getenv("SONATYPE_PASSWORD"))
    }
  }
}

configure<JReleaserExtension> {
  project {
    authors.set(listOf("c-fraser"))
    license.set("Apache-2.0")
    inceptionYear.set("2022")
    description.set("Graph data structure for Kotlin")
    links { homepage.set("https://github.com/c-fraser/connekt") }
  }

  release {
    github {
      repoOwner.set("c-fraser")
      overwrite.set(true)
      token.set(System.getenv("GITHUB_TOKEN").orEmpty())
      changelog {
        formatted.set(Active.ALWAYS)
        format.set("- {{commitShortHash}} {{commitTitle}}")
        contributors.enabled.set(false)
        for (status in listOf("added", "changed", "fixed", "removed")) {
          labeler {
            label.set(status)
            title.set(status)
          }
          category {
            title.set(status.capitalized())
            labels.set(listOf(status))
          }
        }
      }
    }
  }
}

tasks @Suppress("unused") {
  withType<Jar> { manifest { attributes("Automatic-Module-Name" to "io.github.cfraser.connekt") } }

  withType<Test> {
    testLogging {
      showExceptions = true
      showStandardStreams = true
      events = setOf(TestLogEvent.FAILED, TestLogEvent.PASSED)
      exceptionFormat = TestExceptionFormat.FULL
    }
  }

  withType<DokkaTask> {
    pluginConfiguration<DokkaBase, DokkaBaseConfiguration> {
      footerMessage = "Copyright &copy; 2022 c-fraser"
    }
  }

  val setupDocs by registering {
    dependsOn(dokkaHtml)
    doLast {
      copy {
        from(dokkaHtml.get().outputDirectory)
        into(layout.projectDirectory.dir("docs/api"))
      }
      val docs = rootDir.resolve("docs/index.md")
      rootDir.resolve("README.md").copyTo(docs, overwrite = true)
      docs.writeText(
          docs
              .readText()
              // unqualify docs references
              .replace(Regex("\\(docs/.*\\)")) { it.value.replace("docs/", "") }
              // remove inline TOC
              .replace(
                  Regex(
                      "<!--- TOC -->[\\s\\S]*<!--- END -->[\\n|\\r|\\n\\r]",
                      RegexOption.MULTILINE,
                  ),
                  "",
              )
      )
    }
  }

  withType<KotlinTest> { failOnNoDiscoveredTests.set(false) }

  val knit by getting

  val jvmTest by
      getting(Test::class) {
        dependsOn(knit)
        useJUnitPlatform()
      }

  val detekt by getting(Detekt::class)

  val spotlessKotlin by
      getting(SpotlessTask::class) {
        mustRunAfter(
            provider {
              filter { task ->
                task.name.contains("compile", ignoreCase = true) ||
                    task.name.contains("metadata", ignoreCase = true) ||
                    task.name.contains("commonMain", ignoreCase = true) ||
                    task.name.contains("test", ignoreCase = true)
              }
            }
        )
      }
  val spotlessKotlinGradle by getting(SpotlessTask::class) { mustRunAfter(spotlessKotlin) }

  spotlessApply { dependsOn(detekt) }
}
